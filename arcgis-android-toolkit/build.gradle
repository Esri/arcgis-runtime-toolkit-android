/*
 * Copyright 2019 Esri
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'com.android.library'
    id 'kotlin-android-extensions'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.dokka-android'
}

android {
    compileSdkVersion droid.targetSdk
    buildToolsVersion droid.buildTools

    defaultConfig {
        minSdkVersion droid.minSdk
        targetSdkVersion droid.targetSdk
        versionCode project.property("versionCode")
        versionName project.property("versionName")
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            // Required to run Instrumented tests in API 19. Only applicable to debug variants as tests are only run
            // on debug variants.
            multiDexKeepProguard file("$rootProject.projectDir/proguard-multidex-rules.pro")
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    jdkVersion = 8 // Used for linking to JDK

    // This just mutes a warning that spams the console: `Can't find node by signature` but still cannot link
    // external classes in KDocs
    // https://github.com/Kotlin/dokka/issues/269#issuecomment-468733943
    externalDocumentationLink {
        packageListUrl = new URL("https://developers.arcgis.com/android/latest/api-reference/")
        url = new URL("https://developers.arcgis.com/android/latest/api-reference/")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines"
    implementation "com.android.support:appcompat-v7:$droid.support"
    implementation "com.android.support.constraint:constraint-layout:$droid.constraintLayout"
    implementation "com.android.support:multidex:$droid.multiDex"
    implementation "android.arch.lifecycle:runtime:$droid.lifecycle"
    api "android.arch.lifecycle:common-java8:$droid.lifecycle"
    api "com.google.ar.sceneform.ux:sceneform-ux:$droid.arCore"
    api "com.google.ar.sceneform:core:$droid.arCore"
    // fixing failing D8 https://github.com/google-ar/sceneform-android-sdk/issues/575
    implementation "com.google.ar.sceneform:animation:$droid.arCore"
    api "com.esri.arcgisruntime:arcgis-android:$arcgis"
    testImplementation "junit:junit:$test.jUnit"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$test.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
