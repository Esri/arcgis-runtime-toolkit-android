plugins {
    id 'com.android.library'
    id 'kotlin-android-extensions'
    id 'kotlin-android'
    id 'com.jfrog.artifactory'
    id 'android-maven'
    id 'org.jetbrains.dokka-android'
}
apply from: "$rootProject.projectDir/versions.gradle"

group = GROUP
version = VERSION_NAME

android {
    compileSdkVersion droid.targetSdk
    buildToolsVersion droid.buildTools

    defaultConfig {
        minSdkVersion droid.minSdk
        targetSdkVersion droid.targetSdk
        versionCode 100
        versionName "100.2.0"
        multiDexEnabled true
        testInstrumentationRunner "com.esri.arcgisruntime.toolkit.MultiDexTestRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

gradle.projectsEvaluated {
    artifactoryPublish.dependsOn assembleRelease
}

// custom configurations
// published is for artifactory
configurations {
    published
}

// snapshots publish to snapshots repo, release publish to arcgis repo
String localRepo = version.endsWith('SNAPSHOT') ? 'snapshots-local' : 'arcgis-local'

// add apache licese to POM
configure(install.repositories.mavenInstaller) {
    pom.project {
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
    }
}

artifactory {
    def artifactoryUsername = project.hasProperty('ARTIFACTORY_USER') ? ARTIFACTORY_USER : null
    def artifactoryPassword = project.hasProperty('ARTIFACTORY_PASSWORD') ? ARTIFACTORY_USER : null

    contextUrl = ARTIFACTORY_URL

    // disable publish task if artifactory credentials have not been set
    artifactoryPublish.enabled = artifactoryUsername && artifactoryPassword

    publish {
        repository {
            repoKey = localRepo //The artifactory repository key to publish to
            if (artifactoryUsername) {
                username = ARTIFACTORY_USER //The publisher user name, property taken from gradle.properties
            }
            if (artifactoryPassword) {
                password = ARTIFACTORY_PASSWORD //The publisher password, property taken from gradle.properties
            }
        }
        defaults {
            // artifacts to be published to Artifactory.
            publishConfigs('archives', 'published')
            publishArtifacts = true
            publishPom = true //Publish generated POM files to Artifactory (true by default)
            publishIvy = false //Publish generated Ivy descriptor files to Artifactory (true by default)
        }
    }
    resolve {
        repoKey = 'arcgis' //The Artifactory (preferably virtual) repository key to resolve from
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
    jdkVersion = 8 // Used for linking to JDK

    // This just mutes a warning that spams the console: `Can't find node by signature` but still cannot link
    // external classes in KDocs
    // https://github.com/Kotlin/dokka/issues/269#issuecomment-468733943
    externalDocumentationLink {
        packageListUrl = new URL("https://developers.arcgis.com/android/latest/api-reference/")
        url = new URL("https://developers.arcgis.com/android/latest/api-reference/")
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$droid.support"
    implementation "com.esri.arcgisruntime:arcgis-android:$arcgis"
    testImplementation "junit:junit:$test.jUnit"
    androidTestImplementation 'androidx.multidex:multidex:2.0.1'
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$test.espresso", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}